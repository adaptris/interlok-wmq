import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.*

plugins {
  id "java-library"
  id "maven-publish"
  id "jacoco"
  // Manage optional dependencies in maven pom.
  id "nebula.optional-base" version "7.0.0"
  id "com.github.spotbugs" version "6.1.2"
  id "org.sonarqube" version "6.0.1.5171"
  id "org.owasp.dependencycheck" version "11.1.1"
  id "io.freefair.lombok" version "8.11"
}

ext {
  interlokCoreVersion = project.findProperty('interlokCoreVersion') ?: '5.0-SNAPSHOT'
  releaseVersion = project.findProperty('releaseVersion') ?: '5.0-SNAPSHOT'
  nexusBaseUrl = project.findProperty('nexusBaseUrl') ?: 'https://nexus.adaptris.net/nexus'
  mavenPublishUrl = project.findProperty('mavenPublishUrl') ?: nexusBaseUrl + '/content/repositories/snapshots'
  javadocsBaseUrl = nexusBaseUrl + "/content/sites/javadocs/com/adaptris"
  repoUsername = project.findProperty('repoUsername') ?: 'unknown'
  repoPassword = project.findProperty('repoPassword') ?: 'unknown'
  defaultNexusRepo = project.findProperty('defaultNexusRepo') ?: 'unknown'
  offlineJavadocPackageDir = new File(project.buildDir, "offline-javadoc-packages")

  delombokTargetDir = new File("${project.projectDir}/src/main/generated")

  interlokJavadocs= project.hasProperty('interlokJavadocs') ? project.getProperty('interlokJavadocs') : javadocsBaseUrl + "/interlok-core/" + interlokCoreVersion
  interlokCommonJavadocs= project.hasProperty('interlokCommonJavadocs') ? project.getProperty('interlokCommonJavadocs') : javadocsBaseUrl + "/interlok-common/" + interlokCoreVersion
  componentName='Interlok Messaging/WebsphereMQ'
  componentDesc = "Use IBM WebsphereMQ as your messaging platform"
  organizationName = "Adaptris Ltd"
  organizationUrl = "http://interlok.adaptris.net"
  slf4jVersion = "2.0.16"
  mockitoVersion = "5.2.0"
  jmockVersion = "2.13.1"
  jsonOrgVersion = "20241224"
  mqVersion = "9.4.1.1"
}

ext.buildUtil = [
  hostname: { ->
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
      return System.getenv("COMPUTERNAME")
    }
    return System.getenv("HOSTNAME")
  },

  gitBranchNameOrTimestamp: { branchName ->
    if (branchName.equals("HEAD") || branchName.equals("develop") || branchName.startsWith("release")) {
      return new Date().format('HH:mm:ss z');
    }
    return branchName;
  },

  buildInfo: { ->
   new ByteArrayOutputStream().withStream { os ->
      exec {
        executable = "git"
        args = ["rev-parse", "--abbrev-ref", "HEAD"]
        standardOutput = os
      }
      def branchName = os.toString().replaceAll("\r", "").replaceAll("\n", "").trim();
      return buildUtil.gitBranchNameOrTimestamp(branchName);
    }
  },
  // If graphviz is installed via scoop, plantuml doesn't find it because it's not
  // in its expected "location(s)" it searches for c:\*\graphviz**\dot.exe
  // so windows we'd expect to have the GRAPHVIZ_DOT env defined 
  // On Linux we should be able to find it via the path.
  hasGraphViz: { ->
    def app = "dot"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
      app = app + ".exe"
    }
    return System.getenv("GRAPHVIZ_DOT") !=null ||  System.getenv("PATH").split(File.pathSeparator).any{
      java.nio.file.Paths.get("${it}").resolve(app).toFile().exists()
    }
  },

  is_ci_pipeline: { ->
    return System.getenv().containsKey("CI");
  },

  testResourcesDir: { ->
    return "${project.projectDir}/src/test/resources".replaceAll("\\\\", "/")
  },

  propertyTemplate: { dir, filename ->
    def file = new File(dir, filename + "." + buildUtil.hostname())
    if (file.exists()) {
      return file.getName()
    }
    return filename
  },

  unitTestTemplate: {
    return buildUtil.propertyTemplate(buildUtil.testResourcesDir(), "unit-tests.properties.template")
  }

]

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
group   = 'com.adaptris'
version = releaseVersion
def versionDir = "$buildDir/version"
lombok.disableConfig = true

sonar {
  properties {
    property "sonar.projectKey", "adaptris_interlok-wmq"
    property "sonar.organization", "adaptris-org"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

// Disable gradle module generation since we probably don't want
// xxx.module files in your repository.
allprojects {
  tasks.withType(Jar) {
    metaInf {
      from ("$rootDir") {
        include 'LICENSE*'
        include 'NOTICE*'
      }
    }
  }
  tasks.withType(GenerateModuleMetadata) {
    enabled = false
  }
}

repositories {
  mavenCentral()
  if (defaultNexusRepo != "unknown") {
    maven {
      url "$defaultNexusRepo"
      allowInsecureProtocol true
    }
  }
  maven { url "$nexusBaseUrl/content/groups/public" }
  maven { url "$nexusBaseUrl/content/groups/interlok" }
}

configurations {
  javadoc {}
  umlDoclet {}
  offlineJavadocPackages {}
  all*.exclude group: 'c3p0'
  all*.exclude group: 'commons-logging'
  all*.exclude group: 'javamail'
  all*.exclude group: 'javax.mail', module: 'mail'
  all*.exclude group: 'org.glassfish.hk2.external'
  all*.exclude group: 'xalan', module: 'xalan'
  all*.exclude group: 'net.sf.saxon', module: 'saxon'
  all*.exclude group: 'stax', module: 'stax-api'
  all*.exclude group: 'org.codehaus.woodstox'
  all*.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.mail.glassfish'
  // INTERLOK-3197 exclude old javax.mail
  all*.exclude group: 'com.sun.mail', module: 'javax.mail'
  all*.exclude group: 'javax.validation', module: 'validation-api'
  all*.exclude group: 'javax.activation', module: 'activation'
  all*.exclude group: 'javax.activation', module: 'javax.activation-api'

  // INTERLOK-3740 switch from jcraft to com.github.mwiede jsch fork.
  all*.exclude group: 'com.jcraft', module: 'jsch'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

dependencies {
  api ("com.adaptris:interlok-core:$interlokCoreVersion") { changing= true}
  api ("com.adaptris:interlok-common:$interlokCoreVersion") { changing= true}

  implementation ("org.slf4j:slf4j-api:$slf4jVersion")
  implementation ("org.slf4j:jcl-over-slf4j:$slf4jVersion")
  implementation ("com.ibm.mq:com.ibm.mq.allclient:$mqVersion") {
    exclude group: "org.json", module: "json"
  }
  implementation ("org.json:json:$jsonOrgVersion")
  implementation "org.apache.geronimo.specs:geronimo-jms_1.1_spec:1.1.1", optional
  implementation "org.apache.geronimo.specs:geronimo-j2ee-connector_1.6_spec:1.0", optional

  annotationProcessor ("com.adaptris:interlok-core-apt:$interlokCoreVersion") {changing= true}
  umlDoclet("nl.talsmasoftware:umldoclet:2.1.0")

  testImplementation ("org.junit.jupiter:junit-jupiter-engine:5.11.4")
  testImplementation ("org.slf4j:slf4j-simple:$slf4jVersion")
  testImplementation ("com.adaptris:interlok-stubs:$interlokCoreVersion") { changing= true }
  testImplementation ("org.mockito:mockito-core:$mockitoVersion")
  testImplementation ("org.mockito:mockito-inline:$mockitoVersion")

  testImplementation ("org.hamcrest:hamcrest-core:3.0")
  testImplementation ("org.jmock:jmock:$jmockVersion")
  testImplementation ("org.jmock:jmock-junit4:$jmockVersion")
  testImplementation ("org.jmock:jmock-legacy:$jmockVersion")
  testImplementation ("cglib:cglib-nodep:3.3.0")
  testImplementation ("org.objenesis:objenesis:3.4")

  javadoc("com.adaptris:interlok-core-apt:$interlokCoreVersion") { changing= true}
  offlineJavadocPackages ("com.adaptris:interlok-core:$interlokCoreVersion:javadoc@jar") { changing= true}
  offlineJavadocPackages ("com.adaptris:interlok-common:$interlokCoreVersion:javadoc@jar") { changing= true}
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
               "Build-Jdk": System.getProperty('java.version'),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName)
  }
}

sourceSets {
  main {
    output.dir(versionDir, builtBy: 'generateVersion')
  }
}

task copyUnitTestProperties() {
  doLast {
    delete file(buildUtil.testResourcesDir() + "/unit-tests.properties")
    ant.copy(file: buildUtil.testResourcesDir() + "/" + buildUtil.unitTestTemplate(),
             tofile: buildUtil.testResourcesDir() + '/unit-tests.properties') {
      filterset {
         filter(token: "JAVA_HOME", value: System.getProperty("java.home").replaceAll("\\\\", "/"))
         filter(token: "BASE_DIR", value: project.projectDir.getCanonicalPath().replaceAll("\\\\", "/"))
         filter(token: "BUILD_DIR", value: project.buildDir.getCanonicalPath().replaceAll("\\\\", "/"))
      }
    }
  }
}

// Generate the META-INF/adaptris-version file
task generateVersion {
  doLast {
    def versionFile = new File(new File(versionDir, 'META-INF'), 'adaptris-version')
    versionFile.getParentFile().mkdirs()
    ant.propertyfile(file: versionFile) {
      entry(key: 'component.name', value: componentName)
      entry(key: 'component.description', value: componentDesc)
      entry(key: 'groupId', value: project.group)
      entry(key: 'artifactId', value: project.name)
      entry(key: 'build.version', value: project.version)
      entry(key: 'build.date', value: new Date().format('yyyy-MM-dd'))
      entry(key: 'build.info', value: buildUtil.buildInfo())
    }
  }
}
task offlinePackageList(type: Copy) {
  description 'Build an offline package-list for javadocs'
  group 'Documentation'

  project.configurations.offlineJavadocPackages.collect { dependency ->
    from(zipTree(dependency)) {
      into dependency.name
    }
  }
  include "package-list"
  include "element-list"
  into offlineJavadocPackageDir
}

javadoc {
  onlyIf {
    !buildUtil.hasGraphViz()
  }
  configure(options) {
    options.linksOffline(interlokJavadocs, offlineJavadocPackageDir.getCanonicalPath() + "/interlok-core-$interlokCoreVersion-javadoc.jar")
    options.linksOffline(interlokCommonJavadocs, offlineJavadocPackageDir.getCanonicalPath() + "/interlok-common-$interlokCoreVersion-javadoc.jar")
    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addStringOption "tagletpath", configurations.javadoc.asPath
    options.addStringOption "Xdoclint:none", "-quiet"
    title= componentName
  }
}

task umlJavadoc(type: Javadoc) {
  group 'Documentation'
  description 'Build javadocs using plantuml + graphviz + umldoclet, if dot is available'

  onlyIf {
    buildUtil.hasGraphViz()
  }
  source = sourceSets.main.extensions.delombokTask
  classpath = project.sourceSets.main.compileClasspath
  configure(options) {
    options.linksOffline(interlokJavadocs, offlineJavadocPackageDir.getCanonicalPath() + "/interlok-core-$interlokCoreVersion-javadoc.jar")
    options.linksOffline(interlokCommonJavadocs, offlineJavadocPackageDir.getCanonicalPath() + "/interlok-common-$interlokCoreVersion-javadoc.jar")
    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addStringOption "tagletpath", configurations.javadoc.asPath
    options.addStringOption "Xdoclint:none", "-quiet"
    options.docletpath = configurations.umlDoclet.files.asType(List)
    options.doclet = "nl.talsmasoftware.umldoclet.UMLDoclet"
    // Create class & package use pages
    options.addStringOption "use"    
    options.addStringOption "umlImageFormat", "SVG"
    options.addStringOption "umlExcludedTypeReferences", "java.lang.Exception,java.lang.Object,java.lang.Enum,java.lang.annotation.Annotation"
    options.addStringOption "umlJavaBeanPropertiesAsFields"
    options.addBooleanOption "failOnCyclicPackageDependencies", false
    title= componentName
  }
}

jacocoTestReport {
  reports {
    xml.required= true
    html.required= true
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
  classifier = 'examples'
  from new File(buildDir, '/examples')
}

artifacts {
  archives javadocJar
  archives examplesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
        asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.8.0+")
        properties.appendNode("tags", "websphere,webspheremq,jms")
        properties.appendNode("license", "false")
        properties.appendNode("externalUrl", "https://www.ibm.com/docs/en/ibm-mq")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-wmq")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

delombok {
  target = delombokTargetDir
}

task lgtmCompile(type: JavaCompile, dependsOn: delombok) {
  group 'Build'
  description 'Compile for lgtm'

  source = sourceSets.main.extensions.delombokTask
  destinationDirectory= sourceSets.main.java.classesDirectory
  classpath = project.sourceSets.main.compileClasspath
}

task deleteGeneratedFiles(type: Delete) {
  delete file(buildUtil.testResourcesDir() + "/unit-tests.properties"), delombokTargetDir
}

spotbugsMain {
  effort = com.github.spotbugs.snom.Effort.valueOf("MAX")
  reportLevel = com.github.spotbugs.snom.Confidence.valueOf("HIGH")
  reports {
    xml {
      required.set(false)
    }
    html {
      required.set(true)
      stylesheet = 'default.xsl'
    }
  }
  includeFilter = new File("$rootDir/gradle/spotbugs-filter.xml")
}

dependencyCheck  {
  suppressionFiles= [ "https://raw.githubusercontent.com/adaptris/interlok/develop/gradle/owasp-exclude.xml", "$rootDir/gradle/owasp-exclude.xml" ]
  scanConfigurations = [ "api", "compileClasspath", "runtimeClasspath" ]
  formats = [ "HTML", "JUNIT" ]
  junitFailOnCVSS = 7.0
  failBuildOnCVSS = 7.0
  nvd {
    apiKey = System.getenv("NVD_API_KEY")
    delay=project.findProperty("dependencyCheckNvdDelay")
    datafeedUrl=project.findProperty("dependencyCheckNvdDatafeedUrl")
  }
  analyzers {
    assemblyEnabled=false
    knownExploitedURL=project.findProperty('dependencyCheckKnownExploitedURL')
  }
}

test {
  useJUnitPlatform()
}

// disable spotbugsTests which checks our test code..
spotbugsTest.enabled = false

check.dependsOn jacocoTestReport
processTestResources.dependsOn copyUnitTestProperties
clean.dependsOn deleteGeneratedFiles
javadoc.dependsOn offlinePackageList
